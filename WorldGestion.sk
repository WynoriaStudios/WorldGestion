# This resource was developed solely by WynoriaStudios.
# You can reuse the code or the entire file for your own projects, but identify us as the founder of this resource.
# Github Official for this ressource: https://github.com/WynoriaStudios/WorldGestion

# ------------------------------------------------------------------------------------------------------------------------------- #
# ------------------------------------------------------------------------------------------------------------------------------- #
# ------------------------------------------------------------------------------------------------------------------------------- #

options:
	# This option allows you to change the language of this resource.
	# Liste of all languages: English and Français
	Language_name: "English"

	# This option lists worlds that cannot be deleted by default.
	WorldList_NoDelete: "world" or "world_nether" or "world_the_end"

# ------------------------------------------------------------------------------------------------------------------------------- #
# ------------------------------------------------------------------------------------------------------------------------------- #
# ------------------------------------------------------------------------------------------------------------------------------- #

function WorldGestionCMDSystem(sender: sender, type-function: text):

	if {_type-function} = "NO_PERMISSION":

		if {@Language_name} = "English":
			send "&8&l| &cYou are not authorized to access this command." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &cVous n'êtes pas autorisé à accéder à cette commande." to {_sender}


	else if {_type-function} = "HELP":

		send "" to {_sender}
		if {@Language_name} = "English":
			send "&b&lWorldGestion &8x &f&nHELP" to {_sender}
			send "" to {_sender}
			send "&8&l| &f/wg &binfo &8&l» &7Get the list of available information." to {_sender}
			send "&8&l| &f/wg &breload&7/&brestart &8&l» &7Get a script restart." to {_sender}
			send "&8&l| &f/wg &bhelp &8&l» &7Get the list of available commands." to {_sender}

		else if {@Language_name} = "Français":
			send "&b&lWorldGestion &8x &f&nAIDE" to {_sender}
			send "" to {_sender}
			send "&8&l| &f/wg &binfo &8&l» &7Obtenir la liste des informations disponibles." to {_sender}
			send "&8&l| &f/wg &breload&7/&brestart &8&l» &7Obtenir un redémarrage du script." to {_sender}
			send "&8&l| &f/wg &bhelp &8&l» &7Obtenir la liste des commandes disponibles" to {_sender}


	else if {_type-function} = "INFO":

		send "" to {_sender}
		if {@Language_name} = "English":
			send "&b&lWorldGestion &8x &f&nINFORMATION" to {_sender}
			send "" to {_sender}
			send "&8&l| &fName of script: &b&lWorldGestion" to {_sender}
			send "&8&l| &fDeveloper of script: &b&lWynoriaStudios &7(&dWynoria's Studios&7)" to {_sender}
			send "&8&l| &fVersion of script: &a1.0.7" to {_sender}
			send "&8&l| &fGithub official of script: &3https://github.com/WynoriaStudios/WorldGestion" to {_sender}
			send "&8&l| &fSpigot official of script: &3https://www.spigotmc.org/resources/worldgestion.116729/" to {_sender}
			send "&8&l| &fWiki official of script: &3https://mc-worldgestion.wynoria.com/" to {_sender}

		else if {@Language_name} = "Français":
			send "&b&lWorldGestion &8x &f&nINFORMATIONS" to {_sender}
			send "" to {_sender}
			send "&8&l| &fNom du script: &b&lWorldGestion" to {_sender}
			send "&8&l| &fDéveloppeur du script: &b&lWynoriaStudios &7(&dWynoria's Studios&7)" to {_sender}
			send "&8&l| &fVersion du script: &a1.0.7" to {_sender}
			send "&8&l| &fGithub officiel du script: &3https://github.com/WynoriaStudios/WorldGestion" to {_sender}
			send "&8&l| &fSpigot officiel du script: &3https://www.spigotmc.org/resources/worldgestion.116729/" to {_sender}
			send "&8&l| &fWiki officiel du script: &3https://mc-worldgestion.wynoria.com/" to {_sender}


	else if {_type-function} = "RELOAD_SCRIPT":

		make console execute command "/sk reload WorldGestion.sk"
		if {_sender} is a player:
			send "" to {_sender}

			if {@Language_name} = "English":
				send "&8&l| &6You have successfully restarted the script, check the server &3CONSOLE &6to see if there are any problems detected on your restart." to {_sender}

			else if {@Language_name} = "Français":
				send "&8&l| &6Vous avez redémarré le script avec succès, vérifiez la &3CONSOLE &6du serveur pour voir s'il y a des problèmes détectés lors de votre redémarrage." to {_sender}


function WorldCMDSystem(sender: sender, type-function: text, type-world: text, type-world-2: text):

	if {_type-function} = "NO_CONSOLE_ACCESS":

		if {@Language_name} = "English":
			send "&8&l| &cThis command is only available in-game." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &cCette commande n'est disponible que dans le jeu." to {_sender}


	else if {_type-function} = "NO_PERMISSION":

		if {@Language_name} = "English":
			send "&8&l| &cYou are not authorized to access this command." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &cVous n'êtes pas autorisé à accéder à cette commande." to {_sender}


	else if {_type-function} = "HELP":

		send "" to {_sender}

		if {@Language_name} = "English":
			send "&b&lWorldGestion &8x &f&nHELP" to {_sender}
			send "" to {_sender}
			send "&8&l| &f/world &bcreate &7[&dname of new world&7] &8&l» &7Create a world type." to {_sender}
			send "&8&l| &f/world &bclone &7[&dname of new world&7] &7[&dname of clone world&7] &8&l» &7Clone a world type." to {_sender}
			send "&8&l| &f/world &bdelete &7[&dname of world&7] &8&l» &7Delete a world type." to {_sender}
			send "&8&l| &f/world &bsetspawn &8&l» &7Set spawn in world type." to {_sender}
			send "&8&l| &f/world &bspawn &8&l» &7Teleport in player's world." to {_sender}
			send "&8&l| &f/world &btp&7/&bteleport &7[&dname of world&7] &8&l» &7Teleport in a world type." to {_sender}
			send "&8&l| &f/world &blist &8&l» &7Get the list of all worlds." to {_sender}
			send "&8&l| &f/world &bhelp &8&l» &7Get the list of available commands." to {_sender}

		else if {@Language_name} = "Français":
			send "&b&lWorldGestion &8x &f&nAIDE" to {_sender}
			send "" to {_sender}
			send "&8&l| &f/world &bcreate &7[&dnom d'un nouveau monde&7] &8&l» &7Créer un type de monde." to {_sender}
			send "&8&l| &f/world &bclone &7[&dnom d'un nouveau monde&7] &7[&dnom du clone du monde&7] &8&l» &7Créer le clone d'un type de monde." to {_sender}
			send "&8&l| &f/world &bdelete &7[&dnom d'un monde&7] &8&l» &7Supprimer un type de monde." to {_sender}
			send "&8&l| &f/world &bsetspawn &8&l» &7Définir le type de spawn dans le monde." to {_sender}
			send "&8&l| &f/world &bspawn &8&l» &7Téléportation dans le monde du joueur." to {_sender}
			send "&8&l| &f/world &btp&7/&bteleport &7[&dnom d'un monde&7] &8&l» &7Se téléporter dans un type de monde." to {_sender}
			send "&8&l| &f/world &blist &8&l» &7Obtenir la liste de tous les mondes." to {_sender}
			send "&8&l| &f/world &bhelp &8&l» &7Obtenir la liste des commandes disponibles." to {_sender}


	else if {_type-function} = "WORLD_LIST":

		if {@Language_name} = "English":
			send "&8&l| &6List of all worlds: &f%all worlds%" to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &6Liste de tout les mondes: &f%all worlds%" to {_sender}


	else if {_type-function} = "WORLD_NO_EXIST":

		if {@Language_name} = "English":
			send "&8&l| &cThe world &3%{_type-world}% &cdoes not exist." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &cLe monde &3%{_type-world}% &cn'existe pas." to {_sender}


	else if {_type-function} = "WORLD_EXIST":

		if {@Language_name} = "English":
			send "&8&l| &cThe world &3%{_type-world}% &calready exist." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &cLe monde &3%{_type-world}% &cexiste déjà." to {_sender}



	else if {_type-function} = "WORLD_NO_DELETE":

		if {@Language_name} = "English":
			send "&8&l| &cThe world &3%{_type-world}% &ccan't be removed, because it's a default world." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &cLe monde &3%{_type-world}% &cne peut pas être supprimé, car il s'agit d'un monde par défaut." to {_sender}


	else if {_type-function} = "WORLD_DELETE":

		delete world file for {_type-world}

		if {@Language_name} = "English":
			send "&8&l| &6The world &3%{_type-world}% &6has been deleted successfully." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &6Le monde &3%{_type-world}% &6a été supprimée avec succès." to {_sender}


	else if {_type-function} = "WORLD_CREATE":

		set {db-worldgestion::%uuid of {_sender}%::create_new_world::id_named} to "%{_type-world}%"
		wait 1 tick
		GUINewWorldCreateSystem({_sender}, "MAIN_MENU")

	
	else if {_type-function} = "WORLD_CLONE":

		set {_nw} to a new world creator named "%{_type-world-2}%" to clone world "%{_type-world}%" without saving
		load world from creator {_nw}
			
		if {@Language_name} = "English":
			send "&8&l| &6You have created the world clone &3%{_type-world}% &6with successfully." to {_sender}
			
		if {@Language_name} = "Français":
			send "&8&l| &6Vous avez crée le monde clone &3%{_type-world}% &6avec succès." to {_sender}


	else if {_type-function} = "WORLD_TELEPORT":

		teleport {_sender} to the spawn of world {_type-world}

		if {@Language_name} = "English":
			send "&8&l| &6You have been teleported in the world &3%{_type-world}%&6." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &6Vous avez été téléporté dans le monde &3%{_type-world}%&6." to {_sender}


	else if {_type-function} = "SET_SPAWN_WORLD":

		set the spawn point of "%world of {_sender}%" to location of {_sender}

		if {@Language_name} = "English":
			send "&8&l| &6You have set the spawn point in the world &3%world of {_sender}%&6." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &6Vous avez défini le point de réapparition dans le monde &3%world of {_sender}%&6." to {_sender}


	else if {_type-function} = "SPAWN_WORLD":

		teleport {_sender} to the spawn of world of {_sender}

		if {@Language_name} = "English":
			send "&8&l| &6You have been teleported to the spawn point in this world." to {_sender}

		else if {@Language_name} = "Français":
			send "&8&l| &6Vous avez été téléporté au point de réapparition de ce monde." to {_sender}


function GUINewWorldCreateSystem(player: offline player, type-function: text):

	if {_type-function} = "MAIN_MENU":

		if {@Language_name} = "English":

			set {_result::worldgestion::%uuid of {_player}%::gui_menu::name_menu} to "&8&lCreate new world..."

			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_10} to "%nl%&7Creating a normal world."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_11} to "%nl%&7Creating a nether world."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_12} to "%nl%&7Creating a end world."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_13} to "%nl%&7Creating a flat normal world."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_14} to "%nl%&7Creating a void normal world."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_15} to "%nl%&7Creating a amplified normal world."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_16} to "%nl%&7Creating a amplified nether world."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_19} to "%nl%&7Creating a amplified end world."

			set {_result::worldgestion::%uuid of {_player}%::gui_menu::close_menu} to "&cClose the menu"
	
		else if {@Language_name} = "Français":

			set {_result::worldgestion::%uuid of {_player}%::gui_menu::name_menu} to "&8&lCréer un monde..."

			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_10} to "%nl%&7Créer un monde Overworld."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_11} to "%nl%&7Créer un monde Nether."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_12} to "%nl%&7Créer un monde end."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_13} to "%nl%&7Créer un monde flat."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_14} to "%nl%&7Créer un monde void."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_15} to "%nl%&7Créer un monde oveworld amplifié."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_16} to "%nl%&7Créer un monde nether amplifié."
			set {_result::worldgestion::%uuid of {_player}%::gui_menu::slot_19} to "%nl%&7Créer un monde end amplifié."

			set {_result::worldgestion::%uuid of {_player}%::gui_menu::close_menu} to "&cFermer le menu"


		set (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to chest inventory with 6 rows named "%{_result::worldgestion::%uuid of {_player}%::gui_menu::name_menu}%"

		set slot 10 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to grass block named "&aOverWorld" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_10}%"
		set slot 11 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to netherrack named "&aNether" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_11}%"
		set slot 12 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to end stone named "&aThe End" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_12}%"
		set slot 13 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to rooted dirt named "&aFlat" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_13}%"
		set slot 14 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to glass named "&aThe Void" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_14}%"
		set slot 15 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to grass block named "&aAmplified OverWorld" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_15}%"
		set slot 16 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to netherrack named "&aAmplified Nether" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_16}%"
		set slot 19 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to end stone named "&aAmplified The End" with lore "%{_result::worldgestion::%uuid of {_player}%::gui_menu::slot_10}%"

		set slot 49 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to barrier named "%{_result::worldgestion::%uuid of {_player}%::gui_menu::close_menu}%"

		set slot 36, 37, 38, 39, 40, 41, 42, 43 and 44 of (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to light blue stained glass pane	
		open (metadata tag "GUIMenu_CreateNewWorld" of {_player}) to {_player}


command /worldgestion [<text>]:
	aliases: /wg, /worldgestion:worldgestion, /worldgestion:wg
	trigger:
		if sender is a player:
			if player doesn't have permission "worldgestion.command.worldgestion" or "worldgestion.command.*":
				WorldGestionCMDSystem(player, "NO_PERMISSION")
				stop

		if arg-1 isn't set:
			WorldGestionCMDSystem(sender, "HELP")
		else:
			if arg-1 != "info" or "reload" or "restart":
				WorldGestionCMDSystem(sender, "HELP")
			
			else if arg-1 = "info":
				WorldGestionCMDSystem(sender, "INFO")
			
			else if arg-1 = "reload" or "restart":
				WorldGestionCMDSystem(sender, "RELOAD_SCRIPT")


command /world [<text>] [<text>] [<text>]:
	aliases: /worldgestion:world
	trigger:
		if sender is a player:
			if player doesn't have permission "worldgestion.command.world" or "worldgestion.command.*":
				WorldGestionCMDSystem(player, "NO_PERMISSION")
				stop
			else:
				if arg-1 isn't set:
					WorldCMDSystem(player, "HELP", "NONE", "NONE")
				else:
					if arg-1 != "create" or "clone" or "delete" or "setspawn" or "spawn" or "tp" or "teleport" or "list":
						WorldCMDSystem(player, "HELP", "NONE", "NONE")

					else if arg-1 = "create" or "clone" or "delete" or "tp" or "teleport":
						if arg-2 isn't set:
							WorldCMDSystem(player, "HELP", "NONE", "NONE")
						else:

							if arg-1 = "create":
								if (arg-2 parsed as world) is set:
									WorldCMDSystem(player, "WORLD_EXIST", "%arg-2 parsed as world%", "NONE")
								else:
									WorldCMDSystem(player, "WORLD_CREATE", arg-2, "NONE")

							if arg-1 = "clone":
								if (arg-2 parsed as world) isn't set:
									WorldCMDSystem(player, "WORLD_NO_EXIST", arg-2, "NONE")
								else:
									if arg-3 isn't set:
										WorldCMDSystem(player, "HELP", "NONE", "NONE")
									else:
										if (arg-3 parsed as world) is set:
											WorldCMDSystem(player, "WORLD_EXIST", "%arg-2 parsed as world%", "NONE")
										else:
											WorldCMDSystem(player, "WORLD_CLONE", arg-2, arg-3)
						
							else if arg-1 = "delete":
								if (arg-2 parsed as world) isn't set:
									WorldCMDSystem(player, "WORLD_NO_EXIST", arg-2, "NONE")
								else:
									if arg-2 = {@WorldList_NoDelete}:
										WorldCMDSystem(player, "WORLD_NO_DELETE", arg-2, "NONE")
									else:
										WorldCMDSystem(player, "WORLD_DELETE", "%arg-2 parsed as world%", "NONE")

							else if arg-1 = "tp" or "teleport":
								if (arg-2 parsed as world) isn't set:
									WorldCMDSystem(player, "WORLD_NO_EXIST", arg-2, "NONE")
								else:
									WorldCMDSystem(player, "WORLD_TELEPORT", "%arg-2 parsed as world%", "NONE")

					else if arg-1 = "setspawn":
						WorldCMDSystem(player, "SET_SPAWN_WORLD", "NONE", "NONE")

					else if arg-1 = "spawn":
						WorldCMDSystem(player, "SPAWN_WORLD", "NONE", "NONE")
					
					else if arg-1 = "list":
						WorldCMDSystem(player, "WORLD_LIST", "NONE", "NONE")

		else if sender is console:
			WorldCMDSystem(console, "NO_CONSOLE_ACCESS", "NONE", "NONE")


on inventory click:
	if event-inventory = (metadata tag "GUIMenu_CreateNewWorld" of player):
		cancel event

		if name of event-item = "&aOverWorld" or "&aNether" or "&aThe End" or "&aFlat" or "&aThe Void" or "&aAmplified OverWorld" or "&aAmplified Nether" or "&aAmplified The End":
			set {_id_world} to "%{db-worldgestion::%uuid of player%::create_new_world::id_named}%"
			delete {db-worldgestion::%uuid of player%::create_new_world::id_named}
			
			close inventory of player
			set {_nw} to new world creator named "%{_id_world}%"

			if name of event-item = "&aOverWorld":
				set environment option of {_nw} to normal

			else if name of event-item = "&aNether":
				set environment option of {_nw} to nether

			else if name of event-item = "&aThe End":
				set environment option of {_nw} to the end

			else if name of event-item = "&aFlat":
				set environment option of {_nw} to normal
				set world type of {_nw} to flat

			else if name of event-item = "&aThe Void":
				set world type of {_nw} to flat
				set generator settings of {_nw} to "{""biome"": ""minecraft:plains"",""layers"": []}}"
				set should generate structures of {_nw} to false

			if name of event-item = "&aAmplified OverWorld":
				set environment option of {_nw} to normal
				set world type option of {_nw} to amplified

			else if name of event-item = "&aAmplified Nether":
				set environment option of {_nw} to nether
				set world type option of {_nw} to amplified

			else if name of event-item = "&aAmplified The End":

				set environment option of {_nw} to the end
				set world type option of {_nw} to amplified
			
			load world from creator {_nw}
			
			if {@Language_name} = "English":
				send "&8&l| &6You have created the world &3%{_id_world}% &6with successfully." to player
				
			if {@Language_name} = "Français":
				send "&8&l| &6Vous avez crée le monde &3%{_id_world}% &6avec succès." to player

		else if name of event-item = "&cClose the menu" or "&cFermer le menu":
			close inventory of player


on inventory close:
	if {db-worldgestion::%uuid of player%::create_new_world::id_named} is set:
		
		if event-inventory = (metadata tag "GUIMenu_CreateNewWorld" of player):

			if {@Language_name} = "English":
				send "&8&l| &cThe creation of the world &3%{db-worldgestion::%uuid of player%::create_new_world::id_named}% &cwas successfully cancelled." to player

			else if {@Language_name} = "Français":
				send "&8&l| &cLa création du monde &3%{db-worldgestion::%uuid of player%::create_new_world::id_named}% &ca été annulée avec succès." to player
			
			delete {db-worldgestion::%uuid of player%::create_new_world::id_named}


on tab complete of "worldgestion" or "wg" or "worldgestion:worldgestion" or "worldgestion:wg":
	if sender is a player:
		if player has permission "worldgestion.command.worldgestion" or "worldgestion.command.*":
			set tab completions for position 1 to "info", "reload" and "restart"
	else if sender is console:
		set tab completions for position 1 to "info", "reload" and "restart"

on tab complete of "world" or "worldgestion:world":
	if sender is a player:
		if player has permission "worldgestion.command.world" or "worldgestion.command.*":
			set tab completions for position 1 to "create", "clone", "delete", "setspawn", "spawn", "tp", "teleport" and "list"
			if tab arg-1 = "clone" or "tp" or "teleport" or "delete":
				set tab completions for position 2 to all worlds
